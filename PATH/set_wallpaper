#!/usr/bin/fish

# Dependencies list:
#  * colors-git - https://aur.archlinux.org/packages/colors-git/ and http://blog.z3bra.org/2015/06/vomiting-colors.html
#  * nitrogen - https://wiki.archlinux.org/index.php/nitrogen

if not count $argv > /dev/null
    echo "Usage set_wallpaper [IMG_FILE.png]"
    exit 1
end

# First argument must always be an image file
set image_file "$argv[1]"

# Colors amount to generate
set colors_amount 16

# Generates color palette and saves it as array
set generated_palette (colors -en $colors_amount $image_file)

# Alternative: generate 32 colors and take 16 from the middle.
# This approach generates more colorful, but also brighter palettes.
# It is also a little slower than the previous method.
#  set generated_palette (colors -en (math "2 * $colors_amount") $image_file | sed '9,24p;d')

# Alternative: generate 32 colors and take every other.
# This approach generates more toned palettes.
# It is also as slow as the previous method.
#  set generated_palette (colors -en (math "2 * $colors_amount") $image_file | awk 'NR%2==1')

set generated_amount (count $generated_palette)

if test $colors_amount != $generated_amount
    notify-send -u critical "Wallpaper changing error" "Not enough colors could be generated\n$image_file"
    exit 1
end

# Sets image file as a wallpaper
nitrogen --save --set-zoom-fill "$image_file"

for x in (seq $colors_amount)
    # Unsets environment variables
    set -e "GENERATED_PALETTE_$x"

    # Saves generated palette to environment variables
    set -Ux "GENERATED_PALETTE_$x" $generated_palette[$x]

    # Saves generated palette to Xresources
    echo "*generated$x: $generated_palette[$x]" | xrdb -merge
end

# Sets fish colors
fish_set_generated_colors

# Reloads dunst
killall dunst
dunst \
    -cf $GENERATED_PALETTE_1  \
    -cb $GENERATED_PALETTE_15 \
    -nf $GENERATED_PALETTE_10  \
    -nb $GENERATED_PALETTE_1  \
    -lf $GENERATED_PALETTE_7  \
    -lb $GENERATED_PALETTE_1  &

# Restarts i3
i3-msg reload
~/.config/polybar/launch

notify-send "Wallpaper changed" "$image_file"
