#!/usr/bin/fish

# Dependencies list:
#  * colors-git - https://aur.archlinux.org/packages/colors-git/ and http://blog.z3bra.org/2015/06/vomiting-colors.html
#  * feh - https://wiki.archlinux.org/index.php/feh

if not count $argv > /dev/null
    echo "Usage set_wallpaper [IMG_FILE.png]"
    exit 1
end

# First argument must always be an image file
set image_file "$argv[1]"

# Colors amount to generate
set colors_amount 10

# Generates color palette and saves it as array
set generated_palette (colors -en $colors_amount $image_file | sed -e 's/ /a\x1eb/g')
set generated_amount (count $generated_palette)

# If there was an error and colors were not generated
if test $colors_amount != $generated_amount
    notify-send -u critical "Wallpaper changing error" "Not enough colors could be generated\n$image_file"
    exit 1
end

# Sets image file as a wallpaper
feh --bg-fill --no-xinerama "$image_file"

# Creates temporary Xresources file
set xres_gen /tmp/Xresources_generated
echo "*background: #ffffff" > $xres_gen
echo "*foreground: #bbbbbb" >> $xres_gen

for x in (seq $colors_amount)
    # Unsets environment variables
    set -e "GENERATED_PALETTE_$x"

    # Saves generated palette to environment variables
    set -Ux "GENERATED_PALETTE_$x" $generated_palette[$x]

    # Adds entries to temporary Xresources file
    echo "*generated$x: "$generated_palette[$x] >> $xres_gen
end

# Reloads dunst
killall dunst
dunst \
    -cf $GENERATED_PALETTE_1  \
    -cb $GENERATED_PALETTE_10 \
    -nf $GENERATED_PALETTE_8  \
    -nb $GENERATED_PALETTE_1  \
    -lf $GENERATED_PALETTE_6  \
    -lb $GENERATED_PALETTE_1  &

# Loads generated Xresources file
xrdb $xres_gen

# Restarts i3
i3-msg reload
~/.config/polybar/launch
notify-send "Wallpaper changed" "$image_file"
